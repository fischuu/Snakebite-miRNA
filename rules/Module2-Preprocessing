rule cutadapt_trimming:
    """
    Trimming adapter sequences (CUTADAPT).
    """
    input:
        get_raw_input_read1
    output:
        fastqAdapter="%s/FASTQ/TRIMMED/{rawsamples}_trimmed_onlyAdapter.fastq.gz" % (config["project-folder"]),
        fastq="%s/FASTQ/TRIMMED/{rawsamples}_trimmed.fastq.gz" % (config["project-folder"]),
        wclin="%s/FASTQ/RAW/{rawsamples}.wcl" % (config["project-folder"]),
        wcloutAdapter="%s/FASTQ/TRIMMED/{rawsamples}_trimmed_onlyAdapter.wcl" % (config["project-folder"]),
        wclout="%s/FASTQ/TRIMMED/{rawsamples}_trimmed.wcl" % (config["project-folder"]),
        wccin="%s/FASTQ/RAW/{rawsamples}.wcc" % (config["project-folder"]),
        wccoutAdapter="%s/FASTQ/TRIMMED/{rawsamples}_trimmed_onlyAdapter.wcc" % (config["project-folder"]),
        wccout="%s/FASTQ/TRIMMED/{rawsamples}_trimmed.wcc" % (config["project-folder"])
    log:
        "%s/logs/CUTADAPT/cutadapt.{rawsamples}.log" % (config["project-folder"])
    benchmark:
        "%s/benchmark/CUTADAPT/cutadapt.{rawsamples}.benchmark.tsv" % (config["project-folder"])
    params:
        fastq5pAdapter="%s/FASTQ/TRIMMED/{rawsamples}_trimmed_only5pAdapter.fastq.gz" % (config["project-folder"]),    
        adapter5p=config["params"]["cutadapt"]["adapter5p"],
        adapter3p=config["params"]["cutadapt"]["adapter3p"],
        minLength=config["params"]["cutadapt"]["minLength"],
        qualtrim=config["params"]["cutadapt"]["qualtrim"],
        fiveprimetrim=config["params"]["cutadapt"]["fiveprimetrim"],
        threeprimetrim=config["params"]["cutadapt"]["threeprimetrim"]
    threads: cluster["cutadapt_trimming"]["cpus-per-task"]
    resources:
        time=cluster["cutadapt_trimming"]["time"],
        mem=cluster["cutadapt_trimming"]["mem-per-cpu"]
    singularity: config["singularity"]["cutadapt"]
    shell:"""
      if({params.adapter5p}="")
      then
          cutadapt -g {params.adapter5p} \
                   -j {threads} \
                   -o {params.fastq5pAdapter} {input} &>> {log};
    
          cutadapt -a {params.adapter3p} \
                   -j {threads} \
                   -o {output.fastqAdapter} {params.fastq5pAdapter} &>> {log};
      else
          cutadapt -a {params.adapter3p} \
                   -j {threads} \
                   -o {output.fastqAdapter} {input} &>> {log};
      fi      

      cutadapt --minimum-length {params.minLength} \
               -j {threads} -q {params.qualtrim} --trim-n \
               --cut {params.fiveprimetrim} --cut -{params.threeprimetrim} \
               -o {output.fastq} {output.fastqAdapter} &>> {log};
               
      zcat {input} | wc -l > {output.wclin}
      zcat {output.fastqAdapter} | wc -l > {output.wcloutAdapter}
      zcat {output.fastq} | wc -l > {output.wclout}
      
      zcat {input} | sed -n '2~4p' | wc -c  > {output.wccin}
      zcat {output.fastqAdapter} | sed -n '2~4p' | wc -c  > {output.wccoutAdapter}
      zcat {output.fastq} | sed -n '2~4p' | wc -c  > {output.wccout}
    """
    
rule bash_catFastq:
    """
    Concatenate the different lanes into single files (BASH).
    """
    input:
        get_fastq_for_concatenating_read1
    output:
        fastq="%s/FASTQ/CONCATENATED/{samples}_R1.fastq.gz" % (config["project-folder"]),
        report="%s/FASTQ/CONCATENATED/{samples}_R1.fastq.gz.report" % (config["project-folder"]),
        wclout="%s/FASTQ/CONCATENATED/{samples}_R1.wcl" % (config["project-folder"]),
        wccout="%s/FASTQ/CONCATENATED/{samples}_R1.wcc" % (config["project-folder"])
    log:
        "%s/logs/BASH/catFastq_{samples}.log" % (config["project-folder"])
    benchmark:
        "%s/benchmark/BASH/catFastq_{samples}.benchmark.tsv" % (config["project-folder"])
    params:
       infolder="%s/FASTQ/TRIMMED" % (config["project-folder"]),
       outfolder="%s/FASTQ/CONCATENATED" % (config["project-folder"])
    threads: cluster["__default__"]["cpus-per-task"]
    resources:
        time=cluster["__default__"]["time"],
        mem=cluster["__default__"]["mem-per-cpu"]
    shell:"""
    
        echo "Concatentate following files: " {input}
    
        mkdir -p {params.outfolder}
        cat {input} > {output.fastq} 2> {log}
        ls {input} > {output.report} 2> {log}
        zcat {output.fastq} | wc -l > {output.wclout}
        zcat {output.fastq} | sed -n '2~4p' | wc -c  > {output.wccout}
    """