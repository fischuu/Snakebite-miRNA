rule report__preprocess:
    """Render the preprocess module PDF report: adapter trimming and lane-concatenation overview"""
    input:
        rules.preprocess.input,
    output:
        pdf=PIPELINE_REPORT / "preprocess.pdf",
    log:
        PIPELINE_REPORT / "preprocess.log",
    benchmark:
        PIPELINE_REPORT / "preprocess_benchmark.tsv",
    container:
        docker["r_report"]
    params:
        script=PREPROCESS_R,
        pipeline_folder=config["pipeline_folder"],
        features=config["features-file"],
        sample_file=config["sample-file"],
        project_folder=WD,
    threads: esc("cpus", "report__preprocess")
    resources:
        runtime=esc("runtime", "report__preprocess"),
        mem_mb=esc("mem_mb", "report__preprocess"),
        cpus_per_task=esc("cpus", "report__preprocess"),
        slurm_partition=esc("partition", "report__preprocess"),
        gres=lambda wc, attempt: f"{get_resources(wc, attempt, 'report__preprocess')['nvme']}",
        attempt=get_attempt,
    retries: len(get_escalation_order("report__preprocess"))
    shell:
        """
        exec > {log} 2>&1
        R -e "project_folder <- '{params.project_folder}'; \
              pipeline_folder <- '{params.pipeline_folder}'; \
              features_file <- '{params.features}'; \
              sample_file <- '{params.sample_file}'; \
              rmarkdown::render('{params.script}', output_format='pdf_document', \
                                 output_file=file.path('{params.project_folder}', '{output.pdf}'))"
        """
